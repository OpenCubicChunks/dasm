plugins {
    id 'java'
    id 'maven-publish'
}

group 'com.github.OpenCubicChunks'
version '1.3.1'


sourceSets {
    api
}

java {
    withSourcesJar()

    registerFeature("api") {
        usingSourceSet(sourceSets.api)
        withSourcesJar()
    }
}

assemble.dependsOn(tasks.named("apiJar"))
assemble.dependsOn(tasks.named("apiSourcesJar"))

jar {
    from sourceSets.api.output
}

sourcesJar {
    from sourceSets.api.allSource
}

sourceSets.main {
    it.compileClasspath += sourceSets.api.output
    it.runtimeClasspath += sourceSets.api.output
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.google.code.gson:gson:2.9.0'
    implementation 'com.google.guava:guava:31.1-jre'

    implementation 'org.apache.commons:commons-lang3:3.12.0'
    implementation 'org.apache.logging.log4j:log4j-api:2.22.1'
    implementation 'org.apache.logging.log4j:log4j-core:2.22.1'

    implementation 'org.ow2.asm:asm:9.3'
    implementation 'org.ow2.asm:asm-tree:9.3'
    implementation 'org.ow2.asm:asm-util:9.3'
    implementation 'org.ow2.asm:asm-commons:9.3'

    implementation 'org.jetbrains:annotations:23.0.0'

    testImplementation('org.junit.jupiter:junit-jupiter-api:5.9.0')
    testImplementation('org.junit.jupiter:junit-jupiter-params:5.9.0')
    testRuntimeOnly('org.junit.jupiter:junit-jupiter-engine:5.9.0')
}

test {
    useJUnitPlatform()

    // Always run tests, even when nothing changed.
    dependsOn 'cleanTest'

    // Show test results.
    testLogging {
        events "passed", "skipped", "failed"
    }
}


publishing {
    publications {
        // Add additional publications (main and main-sources are added by the java plugin)
        dasm(MavenPublication) {
            from components.java
        }
    }
}